class GroupChat
types
  public String = seq1 of char;
  public Date = Date`Date;
values
-- TODO Define values here
instance variables
  protected name: String;
  protected members: set1 of User;
  protected messages: seq of ChatMessage;
  
  -- Messages are ordered by timestamp
  inv forall i1, i2 in set inds messages & i1 < i2 => messages(i1).getTimestamp() <= messages(i2).getTimestamp();
  
operations
  public GroupChat: String * set1 of User ==> GroupChat
      GroupChat(GroupChatName, GroupChatStartingMembers) == (
        name := GroupChatName;
        members := GroupChatStartingMembers;
        messages := [];
        return self;
      )
      pre GroupChatName <> ""
      post name = GroupChatName and members = GroupChatStartingMembers and messages = [];

  public addMember: User ==> ()
      addMember(user) == (
        members := members union {user}
      )
      pre user not in set members
      post members = members~ union {user};

  public removeMember: User ==> ()
      removeMember(user) == (
        members := members \ {user}
      )
      post members = members~ \ {user};

  public sendMessage: ChatMessage ==> ()
      sendMessage(message) == (
        messages := messages ^ [message];
      )
      pre message.getAuthor() in set members
      post messages = messages~ ^ [message];
  
  -- Get chat messages. Optionally, filter by start and/or end date
  public getMessagesBetween: [Date] * [Date] ==> seq of ChatMessage
      getMessagesBetween(startDate, endDate) == (
      	return [messages(i) | 
      						i in set inds messages & 
      						messages(i).getTimestamp() >= (if startDate <> nil then startDate else Date`minimumDate) and
      						messages(i).getTimestamp() <= (if endDate <> nil then endDate else Date`maximumDate)
      				 ]     
      )
      pre startDate <> nil and endDate <> nil => startDate <= endDate and startDate >= Date`minimumDate and endDate <= Date`maximumDate;
	
	public getMessagesWithText: String ==> seq of ChatMessage
			getMessagesWithText(searchText) == is not yet specified
functions
-- TODO Define functiones here
traces
-- TODO Define Combinatorial Test Traces here
end GroupChat