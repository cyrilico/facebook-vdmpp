class Publication is subclass of Message
types
  -- Publication visibility permissions:
  -- Public - Anyone can see
  -- Friends - Only author's friends can see
  -- FriendsOfFriends - Only author's friends and their friends can see
  -- TransitiveConnection - Anyone with some eventual connection to the author (i.e. friends set transitive closure) can see
  public Permissions = <Public> | <Friends> | <FriendsOfFriends> | <TransitiveConnection>
values
-- TODO Define values here
instance variables
	protected static idCounter: nat1 := 1;
	protected id: nat1 := idCounter;
  protected likes: set of User;
  protected permissions: Permissions;
  
operations
  public Publication: User * String * Date * Permissions ==> Publication
      Publication(PublicationAuthor, PublicationContent, PublicationTimestamp, PublicationPermissions) == (
      	idCounter := idCounter + 1;
      	author := PublicationAuthor;
        content := PublicationContent;
        timestamp := PublicationTimestamp;
      	permissions := PublicationPermissions;
      	likes := {};
      	return self;
      )
      pre PublicationContent <> ""
      post idCounter = idCounter~ + 1 and author = PublicationAuthor and content = PublicationContent and timestamp = PublicationTimestamp and likes = {} and permissions = PublicationPermissions;
      
  pure public getId: () ==> nat1
  		getId() == return id
  		post RESULT = id;
  		
  public updatePermissions: Permissions ==> ()
  		updatePermissions(newPermissions) == (
  			permissions := newPermissions;
  		)
  		post permissions = newPermissions;
  
  pure public userHasPermissions: User ==> bool
  		userHasPermissions(user) == (
  			cases permissions:
  			<Public> -> return user not in set author.getBlockedUsers(),
  			<Friends> -> return user in set author.getFriends() union {author} \ author.getBlockedUsers(),
  			<FriendsOfFriends> -> return user in set author.getFriendsOfFriends() union {author} \ author.getBlockedUsers(),
  			<TransitiveConnection> -> return user in set author.getFriendsTransitiveClosure() union {author} \ author.getBlockedUsers(),
  			others -> return user not in set author.getBlockedUsers()
  			end
  		);
  
  pure public getLikes: () ==> set of User
  		getLikes() == return likes
  		post RESULT = likes;
  		
  public like: User ==> ()
  		like(user) == (
  			likes := likes union {user}
  		)
  		pre userHasPermissions(user)
  		post user in set likes and card likes = card likes~ + 1;
 	
 	public removeLike: User ==> ()
 			removeLike(user) == (
 				likes := likes \ {user}
 			)
 			pre user in set likes
 			post user not in set likes and card likes = card likes~ - 1;
functions
-- TODO Define functiones here
traces
-- TODO Define Combinatorial Test Traces here
end Publication