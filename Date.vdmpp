class DateUtils

  types  
    public Date = nat
       inv d == IsValidDate(d div 10000, (d div 100) mod 100, d mod 100);

  values

  functions
      
    public static IsValidDate: nat * nat * nat -> bool
      IsValidDate(year, month, day) ==
        year >= 1 and month >= 1 and month <= 12 and day >= 1 and day <= DaysOfMonth(year, month);
    
    public static IsLeapYear: nat -> bool
      IsLeapYear(year) == 
        year mod 4 = 0 and year mod 100 <> 0 or year mod 400 = 0;

    public static DaysOfMonth: nat * nat -> nat
      DaysOfMonth(year, month) == (
        cases month :
          1, 3, 5, 7, 8, 10, 12 -> 31,
          4, 6, 9, 11 -> 30,
          2 -> if IsLeapYear(year) then 29 else 28
        end
       )
      pre month >= 1 and month <= 12;

    public static MakeDate: nat * nat * nat -> Date
      MakeDate(year, month, day) == 
        year * 10000 + month * 100 + day
      pre IsValidDate(year, month, day);
 
    public static Year: Date -> nat
      Year(d) == 
        d div 10000;
      
    public static Month: Date -> nat
      Month(d) == 
        (d div 100) mod 100;
      
    public static Day: Date -> nat
      Day(d) == 
        d mod 100;          

end DateUtils